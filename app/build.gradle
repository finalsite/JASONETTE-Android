apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.finalsite.finalsiteacademy2"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 4100400
        versionName "4.10.4"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    bundle {
        language {
            enableSplit false
        }
    }

    if (file("../../fastlane/keys/keystore.properties").exists()) {

        Properties props = new Properties()

        if (file("../../config/keys/keystore.properties").exists()) {
            props.load(new FileInputStream(file("../../config/keys/keystore.properties")))
        } else {
            props.load(new FileInputStream(file("../../fastlane/keys/keystore.properties")))
        }

        signingConfigs {
            release {
                storeFile file(props['RELEASE_STORE_FILE'])
                storePassword props['RELEASE_STORE_PASSWORD']
                keyAlias props['RELEASE_STORE_KEY_ALIAS']
                keyPassword props['RELEASE_STORE_KEY_PASSWORD']
            }
        }
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    task mergeConfigurations(type:Exec) {
        workingDir '../../'
        commandLine 'sh', '-l', './fastlane/scripts/android_merge_script.sh'
    }

    preBuild.dependsOn(mergeConfigurations)

    lintOptions {
        disable "ResourceType"
    }
    // Gradle automatically adds 'android.test.runner' as a dependency.
    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
	androidTestImplementation 'androidx.test.espresso:espresso-intents:3.1.0'
    androidTestImplementation 'tools.fastlane:screengrab:2.0.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.2.0'

    implementation 'com.google.firebase:firebase-messaging:18.0.0'

    debugImplementation 'com.facebook.stetho:stetho:1.5.0'
    debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    debugImplementation 'com.facebook.stetho:stetho-timber:1.5.0'

    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'

    // noinspection GradleCompatible
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    implementation 'com.eclipsesource.j2v8:j2v8:4.8.4@aar'
    implementation 'com.aurelhubert:ahbottomnavigation:2.0.2'
    implementation 'commons-lang:commons-lang:2.6'
    implementation 'com.github.scribejava:scribejava-apis:4.0.0'
    implementation 'com.yqritc:recyclerview-flexibledivider:1.4.0'
    implementation 'com.commonsware.cwac:cam2:0.7.4'
    implementation 'com.github.maks:AndroidAudioRecorder:0.3.0-jasonette'
    implementation 'com.github.florent37:singledateandtimepicker:1.0.8'
    implementation 'com.jakewharton.timber:timber:4.5.1'

    //noinspection UseOfBundledGooglePlayServices
    implementation 'com.google.android.gms:play-services-vision:17.0.2'
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    implementation 'com.google.android.gms:play-services-maps:16.0.0'

    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-support-core-ui:1.0.0'

}

apply plugin: 'com.google.gms.google-services'
